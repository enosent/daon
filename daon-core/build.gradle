buildscript {

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.1"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.4.5"
    }
}

apply plugin: 'idea'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'com.google.protobuf'
apply plugin: 'me.champeau.gradle.jmh'

ext {
    jarName = 'daonCore'
}

shadowJar {
    baseName = "${jarName}"
    classifier = null
    version = null
    zip64 = true
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }

    generatedFilesBaseDir = "$projectDir/gen"
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    }
}

javadoc {
    exclude '**/lucene/**'

    options {
        windowTitle = "Daon Javadoc ${project.version}"
    }
    options.addStringOption('top').value = '<a href="https://github.com/rasoio/daon">daon analyzer</a><h2 class="title" style="padding-top:40px">한글 형태소 분석기</h2>'
}

jmh {
    include = ['AnalyzerPerf']
    fork = 1
    iterations = 10
//    jmhVersion = '1.19'
//	profilers = ['gc']
    threads = 3
    warmup = '1s'
    warmupBatchSize = 1
    warmupIterations = 10
}


task buildDaonCore {

    doLast {
        copy {
            from "${project.buildDir}/libs/${jarName}.jar"
            into "${rootProject.projectDir}/distributions"
        }
    }
}

buildDaonCore.dependsOn(shadowJar)

configure([buildDaonCore]) {
    group = 'build'
}

dependencies {
    compile "ch.qos.logback:logback-classic:1.1.7"
    compile "com.google.protobuf:protobuf-java:3.0.0"

    testCompile 'junit:junit:4.+'
}



